group = 'com.mantono'
version = '0.2.0'
description = 'description of project'

defaultTasks 'test'

buildscript {
    ext.kotlin_version = '1.3.10'
    ext.jvm_version = '1.8'
    ext.junit_platform_version = '1.2.0'

    repositories {
        jcenter()
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url "http://dl.bintray.com/kotlin/kotlin-eap" }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.junit.platform:junit-platform-gradle-plugin:$junit_platform_version"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:0.9.17"
    }
}

apply plugin: 'org.jetbrains.dokka'
apply plugin: 'kotlin'
apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'org.jetbrains.kotlin.jvm'
apply plugin: 'idea'
apply plugin: "org.junit.platform.gradle.plugin"

sourceCompatibility = jvm_version

repositories {
    mavenCentral()
    jcenter()
    mavenLocal()
}

dependencies {
    compile group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib-jdk8', version: kotlin_version
    compile group: 'org.jetbrains.kotlinx', name: 'kotlinx-coroutines-jdk8', version: '1.0.1'
    compile group: 'org.jetbrains.kotlinx', name: 'kotlinx-coroutines-core', version: '1.0.1'
    implementation 'io.github.microutils:kotlin-logging:1.6.20'
    implementation group: 'org.apache.kafka', name: 'kafka-clients', version: '2.0.0'
    runtime 'ch.qos.logback:logback-classic:1.2.3'
    testCompile 'org.junit.jupiter:junit-jupiter-api:5.3.0'
    testRuntime 'org.junit.jupiter:junit-jupiter-engine:5.3.0'
}

junitPlatform {
    platformVersion = junit_platform_version
}

compileKotlin {
    kotlinOptions.jvmTarget = "$jvm_version"
    kotlinOptions {
        languageVersion = "1.3"
    }
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "$jvm_version"
}

dokka {
    outputFormat = 'html'
    outputDirectory = "doc"
    jdkVersion = 8
}

task dokkaJavadoc(type: org.jetbrains.dokka.gradle.DokkaTask) {
    outputFormat = 'javadoc'
    outputDirectory = javadoc.destinationDir
    inputs.dir 'src/main/kotlin'
}

// Important: All classes containing test cases must match the
// the regex pattern "^.*Tests?$" to be picked up by the junit-gradle plugin.
sourceSets {
    main.kotlin.srcDirs += 'src/main/kotlin'
    test.kotlin.srcDirs += 'src/test/kotlin'
}

task run(type: JavaExec) {
    main = 'com.mantono.pyttipanna.GeneratePasswordKt'
    classpath = sourceSets.main.runtimeClasspath
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

task wrapper(type: Wrapper) {
    description = 'Generates gradlew[.bat] scripts for faster execution'
    gradleVersion = '4.10.2'
}